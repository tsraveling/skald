
-- This is a comment!

-- These are variable initializations
~ some_var = 3 -- This is a raw value
~ some_other = :call_method() -- this initialized the variable with a call to an external method

-- These are inherited variables, with local testing values
< imported_var = 0
< player_name = "Billy"

-- This is a testbed, for testing a script with alternative configurations
@testbed some_testbed
  some_var = 10
  some_other = 9
@end

@testbed AnotherBed
  player_name = "Salmon"
  some_var = 11.2
  foo = true
@end


-- This is a block. A block starts with a tag:
#test_block

-- It has "text beats":
First beat

-- Some of which can inject values:
Second beat with an {injection}.

-- Including via the use of a ternary:
This one has a normal ternary, and {some_flag ? "yarrrrr!" : "narrrrr."} is the result.

-- Or a "switch ternary":
Switch ternary: {pet_type ? [1: "cat", 2: "dog", 3: "camel"]}

This is a normal text block with some ops on it
  -> some_transition -- These intended commands following beats or choices are "operations"
  ~ some_var = another_var

-- The single asterisk is a "logic beat" with logic but no text
-- The (? x) syntax is a conditional

* (? should_transfer)
  ~ test = 5
  GO subfolder/other_module.ska

* (? should_transfer_directed)
  GO subfolder/other_module.ska -> some_tag

(? show_conditional) A conditional can precede a beat as well.

-- This logic beat has an EXIT command on it, which will exit the script and return the optional argument
* (else)
  EXIT 14


-- This pair of logic beats has an (else) logic beat on the second; it will fire if the precedind conditional does not.
* (? is_true) -> one_way
* (else) ~ got_lost = true

-- This beat has an inline comment on it
Third {-- with inline comment} test

-- These are various kinds of conditionals
(? :method_check() and !:arg_check(a, b, c)) This one is conditional with a truthy value!
(? check_val and !falsy_check and (more_check > 10 or less_check<= 9)) This one is a combo clause
(? !check_val) This one shows up if check val is not true
(? val_a and val_b and val_c and val_d) This one checks a long and clause.

-- Some beats are attributed, with an identifier + `:` preceding the beat text
some_tag: This one is attributed!

-- In a given block, you always have the block tag, followed by some number of beats (above), optionally followed by a set of choices (below). These start with
-- >, have choice text, and then have operations indented on following lines. The move command (->) may follow on the same line.

> Bye!
  EXIT 14
> Next please!
  GO next_chapter.ska -- This GO moves us to a different Skald file.
> This is a choice
> (? some_condition = "test") This choice is conditional
> This one goes -> to_school
> This one has operations in lines
  -> to_park
  :baseMethod() -- this one has a comment on it!
  :methodWithArg(5)
  :methodWithAllAgs(5, true, 2.3, false, "some string content!", a_variable)
> This one mutates
  ~ equate = :some_method(with_arg)
  ~ add += 3
  ~ add += :method_output("cat")
  ~ subtract -= 5
  ~ flip =!
  ~ equate_var = some_other_var

